{//;inject functions}
{inject;{get;_gitcmd.functions}}

{//;get input}
{set;!~count;{replace;{numformat;{flag;c};};NaN;1}}
{set;!~reason;{if;{flag;r};==;{null};No reason given;{flag;r}}}
{set;!~modid;{userid}}

{//;get user to pardon}
{fallback;}
{suppresslookup}
{set;!~userid;{userid;{flag;_}}}

{//;check if valid user input}
{switch;{get;~userid};
["no input given","","{get;~modid}"];
{//;invalid user input}
{output;{embed;{embedbuild;
color:{func.dlcolors;red};
title:Pardon Error;
description:Input does not contain valid user;
}}}
{return}
}
{fallback}

{//;pardon user}
{set;!~warns;{pardon;{get;~userid};{replace;{parseint;{get;~count}};NaN;};{get;~reason}}}

{//;check if pardon succeeded}
{if;{parseint;{get;~warns}};==;NaN;
{//;pardon failed}
{output;{embed;{embedbuild;
color:{func.dlcolors;red};
title:Pardon Error;
description:Failed to pardon {usermention;{get;~userid}}.  {get;~warns};
}}}
{return}
}

{//;modlog}
{execcc;modlog;pardon;{get;~userid};{userid};{get;~reason};{get;~count}}
