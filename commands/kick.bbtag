{//;inject functions}
{inject;{get;_gitcmd.functions}}

{//;get input}
{set;!~reason;{if;{flag;r};==;{null};No reason given;{flag;r}}}
{//;set modid based on commandname}
{switch;{commandname};
["kick"];
{set;!~modid;{userid}};
{//;set modid to bot's id when ran outside of kick command}
{set;!~modid;{exec;botid}}
}

{//;check if message is reply}
{suppresslookup}
{switch;{commandname};
["kick"];
{set;!~kmessageid;{messagereply}}
}

{//;get user to kick}
{if;{get;~kmessageid};!=;{null};
{set;!~userid;{messagesender;{get;~kmessageid}}};
{set;!~userid;{userid;{flag;_}}}
}

{//;check if valid user input}
{switch;{get;~userid};
["no input given","","{get;~modid}"];
{//;invalid user input}
{output;{embed;{embedbuild;
color:{func.dlcolors;red};
title:Kick Error;
description:Input does not contain valid user;
}}}
{return}
}

{//;DM user before kicking}
{void;{dm;{get;~userid};**You have been kicked from {guildname} by <@{get;~modid}>!**{newline}**Reason**: {get;~reason}}}

{//;kick user}
{set;!~kicked;{kick;{get;~userid};{get;~reason}}}

{//;check if kick succeeded}
{if;{get;~kicked};startswith;`;
{//;kick failed}
{output;{embed;{embedbuild;
color:{func.dlcolors;red};
title:Kick Error;
description:Failed to kick {usermention;{get;~userid}}.  {get;~kicked};
}}}
{return}
}

{//;modlog}
{execcc;modlog;kick;{get;~userid};{get;~modid};{get;~reason}}
